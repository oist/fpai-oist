/* vim: set ts=2 sw=2 et fenc=utf-8 ff=unix : */
import aQute.bnd.osgi.Jar

buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:${bnd_version}"
  }
}
apply plugin: 'nebula-aggregate-javadocs'

subprojects {
  if (plugins.hasPlugin('biz.aQute.bnd')) {

    repositories {
      jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    javadoc {
      failOnError = false
    }

    jacoco {
      toolVersion = '0.7.9'
      reportsDir = file("$buildDir/jacoco")
    }

    jacocoTestReport {
      additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
      reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/jacoco/html"
      }
    }

    apply plugin: 'checkstyle'
    checkstyle {
      configFile = file("${cnf.projectDir}/checkstyle/configuration.xml")
      ignoreFailures = true
      showViolations = false
      configProperties.put("samedir", "${cnf.projectDir}/checkstyle")
    }

    task cleanGenerated(type: Delete) {
      delete 'generated'
    }
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}
